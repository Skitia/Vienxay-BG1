
IF
	Global("chapter","global",13)
	GlobalTimerExpired("bd_jail_visitors_timer","bd0104")
	Global("X32VienRomanceActive","GLOBAL",2) // Kale/Helga/Emily use a romance check. Recorder should have her epilogue one here, which will always be true.
	Global("bd_jail_visitors","bd0104",4) //Post Corwin. Recorder should use 0 and be pre Corwin.
	!Dead("X3Vien")  
THEN
	RESPONSE #100
		SetGlobal("bd_jail_visitors","bd0104",5)
		SetGlobalTimer("bd_jail_visitors2_timer","bd0104",TEN_ROUNDS)
		MoveGlobal("bd0104","X3Vien",[720.620])  //coordinates true for all visiting NPCs
		ActionOverride("X3Vien",ChangeAIScript("bdvisit",OVERRIDE)) //this script will handle the "NPC walks up to the PC's prison door and initiates dialogue"
END

	

IF
!Global("chapter","global",13)
Global("X32VienSpawn","GLOBAL",0)
BeenInParty("X3Vien")
THEN
RESPONSE #100
SetGlobal("X32VienSpawn","GLOBAL",1)
MoveGlobal("BD0104","X3Vien",[1568.375])
    ApplySpellRES("bdresurr","X3Vien")
    SmallWait(1)
    ActionOverride("X3Vien",Face(NW))
	ApplySpellRES("bdrejuve","X3Vien")
    ChangeEnemyAlly("X3Vien",NEUTRAL)
    ChangeSpecifics("X3Vien",ALLIES)
    ActionOverride("X3Vien",SetGlobal("X3VienJoined","locals",0))
    ActionOverride("X3Vien",SetGlobal("bd_retreat","locals",0))
    ActionOverride("X3Vien",SaveObjectLocation("LOCALS","bd_default_loc",Myself))
    ActionOverride("X3Vien",ChangeAIScript("X32Vien",OVERRIDE))
    ActionOverride("X3Vien",ChangeAIScript("DEFAULT",CLASS))
    ActionOverride("X3Vien",ChangeAIScript("",RACE))
    ActionOverride("X3Vien",ChangeAIScript("",GENERAL))
    ActionOverride("X3Vien",ChangeAIScript("",DEFAULT))
    ActionOverride("X3Vien",SetDialog("X32Vien"))
	ActionOverride("X3Vien",SetPlayerSound(Myself,@100002,EXISTANCE5))
	Continue()
END

IF 
!Global("chapter","global",13)
Global("X32VienSpawn","GLOBAL",0)
!BeenInParty("X3Vien")
THEN 
RESPONSE #100 
SetGlobal("X32VienSpawn","GLOBAL",1)
CreateCreature("X32Vien",[1568.375],NW)
ActionOverride("X3Vien",MakeGlobal())
END

IF 
!Global("chapter","global",13)
!InParty("X3Vien")
Global("X3VienWhineTimer","GLOBAL",0)
THEN 
RESPONSE #100 
SetGlobalTimer("X3VienWhine","AREA",12)
SetGlobal("X3VienWhineTimer","GLOBAL",1)
END

IF 
!Global("chapter","global",13)
!InParty("X3Vien")
GlobalTimerExpired("X3VienWhine","AREA")
Global("X32VienSpawn","GLOBAL",1)
RandomNum(3,1)
THEN 
RESPONSE #100 
SetGlobalTimer("X3VienWhine","AREA",12)
DisplayStringHead("X3Vien",~Let me out already! Do you know who I am?!~)
END

IF 
!InParty("X3Vien")
!Global("chapter","global",13)
GlobalTimerExpired("X3VienWhine","AREA")
Global("X32VienSpawn","GLOBAL",1)
RandomNum(3,2)
THEN 
RESPONSE #100 
SetGlobalTimer("X3VienWhine","AREA",12)
DisplayStringHead("X3Vien",~This is an outrage. I am a woman to be respected, not caged.~)
END

IF 
!InParty("X3Vien")
!Global("chapter","global",13)
GlobalTimerExpired("X3VienWhine","AREA")
Global("X32VienSpawn","GLOBAL",1)
RandomNum(3,3)
THEN 
RESPONSE #100 
SetGlobalTimer("X3VienWhine","AREA",12)
DisplayStringHead("X3Vien",~Stupid corrupt humans. So jealous of my superiority.~)
END


LOAD_TRA ~Vienxay/translations/English/vien_bg1.tra~

APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~ 
  
// BAELOTH BANTER

ACTION_IF FILE_EXISTS_IN_GAME ~neera.dlg~ THEN BEGIN
 PRINT ~BG:EE detected - Baeloth interdia.2da append - set Baeloth's banter file (BBAELOTH)~
    APPEND ~interdia.2da~ ~BAELOTH BBAELOTH~
    UNLESS ~BAELOTH~
   ACTION_IF NOT (FILE_EXISTS_IN_GAME ~BBAELOTH.dlg~) THEN BEGIN
   COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogue/BBAELOTHFix.d~
  END
END

  ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
    APPEND ~interdia.2da~ ~IMOEN                    _BIMOEN~ UNLESS ~_\(BIMOEN\|bimoen\)~
  END
  /* BGT: Giving Imoen a banter file if the Fixpack isn't installed */
  ACTION_IF NOT (FILE_EXISTS_IN_GAME ~cdbehbla.pro~) AND (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~saradush.mve~ THEN BEGIN
      COPY_EXISTING ~interdia.2da~ ~override~
        SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
      BUT_ONLY_IF_IT_CHANGES
    END ELSE BEGIN
        APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ // fixes SoA version
        UNLESS ~BIMOEN2~
    END
  END


    /* Imoen BIMOEN.DLG rebuild */
  ACTION_IF NOT (FILE_EXISTS_IN_GAME ~_bimoen.dlg~) AND (FILE_EXISTS_IN_GAME ~FW0100.are~) THEN BEGIN
    COMPILE ~Vienxay/Dialogue/x#bimoen_tutu.d~
  END ELSE BEGIN
    ACTION_IF NOT (FILE_EXISTS_IN_GAME ~bimoen2.dlg~) AND (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
      COMPILE ~Vienxay/Dialogue/x#bimoen_bgt.d~
    END
  END

  /* ACTION.IDS and TRIGGER.IDS patching to ToB - thanks, Cam, if you read it */
  /* and GTIMES.IDS, ANIMATE.IDS patching to ToB, courtesy of the BG2 Fix Pack */
  INCLUDE ~Vienxay\library/ids_entries_cleanup.tph~
  
/* Tutu Area Script Assignment Patching: All Areas Script ID'd */
ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
  INCLUDE ~Vienxay\library/tutu_area_script_assign.tph~
END

/* Area Type Flagging */
/* ToSC only: Tutu and BGT */
ACTION_IF (FILE_EXISTS_IN_GAME ~FW1500.are~) OR (FILE_EXISTS_IN_GAME ~ARW500.are~) THEN BEGIN // if TotSC is installed
  COPY_EXISTING ~%IsleofBalduranN%.are~ ~override~
                ~%IsleofBalduranS%.are~ ~override~
                ~%DurlagsTower%.are~ ~override~
                ~%Farmlands%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
  BUT_ONLY_IF_IT_CHANGES
END

  /* FOREST and OUTDOOR: Tutu and BGT */
ACTION_IF (FILE_EXISTS_IN_GAME ~FW0100.are~) OR (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
  COPY_EXISTING ~%FishingVillage%.are~ ~override~
                ~%Peldvale%.are~ ~override~
                ~%LionsWay%.are~ ~override~
                ~%CoastWay%.are~ ~override~
                ~%Larswood%.are~ ~override~
                ~%ShipwrecksCoast%.are~ ~override~
                ~%HighHedge%.are~ ~override~
                ~%MutaminsGarden%.are~ ~override~
                ~%Lighthouse%.are~ ~override~
                ~%RedCanyons%.are~ ~override~
                ~%SouthBeregostRoad%.are~ ~override~
                ~%Ulcaster%.are~ ~override~
                ~%ArchaeologicalSite%.are~ ~override~
                ~%FishermansLake%.are~ ~override~
                ~%NorthNashkelRoad%.are~ ~override~
                ~%LonelyPeaks%.are~ ~override~
                ~%FirewineBridge%.are~ ~override~
                ~%BearRiver%.are~ ~override~
                ~%ValleyoftheTombs%.are~ ~override~
                ~%DryadFalls%.are~ ~override~
                ~%FireLeafForest%.are~ ~override~
                ~%GibberlingMountains%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
    BUT_ONLY_IF_IT_CHANGES

/* OUTDOOR ONLY: Tutu and BGT */
COPY_EXISTING ~%GnollStronghold%.are~ ~override~
              ~%NashkelMines%.are~ ~override~
              ~%FriendlyArmInn%.are~ ~override~
              ~%Temple%.are~ ~override~
              ~%NashkelCarnival%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00000001")
  BUT_ONLY_IF_IT_CHANGES

/* CITY and OUTDOOR */
COPY_EXISTING ~%WyrmsCrossing%.are~ ~override~
              ~%Candlekeep_Ch6%.are~ ~override~
              ~%Gullykin%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00001001")
  BUT_ONLY_IF_IT_CHANGES
END





//AUDIO



LAF HANDLE_AUDIO END
//Spell
COPY	"Vienxay/Spells/X3VSBLT.spl" override
	SAY NAME1 @54
	SAY NAME2 @55
	SAY 0x50 @56
///

COPY ~Vienxay/cre/X3Vien.cre~ ~override/X3Vien.cre~
  SAY NAME1 @1 //Vienxay
  SAY NAME2 @1 //Vienxay
  WRITE_ASCII 0x248 ~X3Vien~ #8  //override script
  WRITE_ASCII 0x280 ~X3Vien~ #32  //DV
  WRITE_ASCII 0x2cc ~X3Vien~ #8   //pre-joining dialogue file
  WRITE_ASCII 0X34 ~VienxayM~ #8  
  WRITE_ASCII 0x3c ~VienxayL~ #8  
  SAY BIO @2
  SAY MORALE @3 // ~Oh goddess, we're all going to die.~
  SAY HAPPY @4 // ~By my art, we have triumphed!~
  SAY UNHAPPY_ANNOYED @5
  SAY UNHAPPY_SERIOUS @6
  SAY UNHAPPY_BREAKING_POINT @7  
  SAY LEADER @8 // ~Everyone gather around me!~
  SAY TIRED @9 // ~Isn't this enough for one day? I need to refresh my spells.~
  SAY BORED @10 // ~Well. This is special.~
  SAY BATTLE_CRY1 @11 // ~There they are! We'll easily destroy them.~
  SAY BATTLE_CRY2 @12 // ~My art will bring you low!~
  SAY BATTLE_CRY3 @13 // ~You will rue the day you invited my attack.~
  SAY BATTLE_CRY4 @14 // ~Prepare to face the judgment of Mystra!~
  SAY BATTLE_CRY5 @15 // ~Trust in Mystra, and bring them down.~
  SAY DAMAGE @16 //~Ugh!~ 
  SAY DYING @17 //~Mystra, take them, not me!~
  SAY HURT @18 // ~Quick, hurry! Protect me!~
  SAY AREA_FOREST @19 // ~The land here is nothing compared to Evermeet.~
  SAY AREA_CITY @20
  SAY AREA_DUNGEON @21
  SAY AREA_DAY @22
  SAY AREA_NIGHT @23
  SAY SELECT_COMMON1 @24 // ~Yes?~
  SAY SELECT_COMMON2 @25 // ~Yes, hello.~
  SAY SELECT_COMMON3 @26 // ~Yes, of course.~
  SAY SELECT_COMMON4 @27 // ~Pay Attention!~
  SAY SELECT_COMMON5 @25 // ~Yes, hello.~
  SAY SELECT_COMMON6 @24 // ~Yes?~
  SAY SELECT_ACTION1 @29 // ~I hardly think that's wise.~
  SAY SELECT_ACTION2 @30  // ~Stand aside, peasant.~
  SAY SELECT_ACTION3 @31 // ~That's not worth my time.~
  SAY SELECT_ACTION4 @32 // ~Of course I can do that.~
  SAY SELECT_ACTION5 @29 // ~I hardly think that's wise.~
  SAY SELECT_ACTION6 @30 // ~Stand aside, peasant.~
  SAY SELECT_ACTION7 @31 // ~That's not worth my time.~
  SAY CRITICAL_HIT @49 //~My wet nurse was tougher!~ 
  SAY CRITICAL_MISS @33 //~Shar take you!~ 
  SAY TARGET_IMMUNE @34 // ~Well. This is what happens when you don't use magic.~
  SAY INVENTORY_FULL @35 // ~This is too much weight. Aren't there hirelings to carry this for us?~  
  SAY SPELL_DISRUPTED @36 // ~What? What just happened? That's inconceivable!~ 
  SAY SET_A_TRAP @37 // ~It's done.~  
  SAY HIDDEN_IN_SHADOWS @38 // ~Do I have to waste an invisibility spell, or do you think you can manage to stay hidden?~ 
  SAY PICKED_POCKET @39 // ~It's done.~ 
  SAY SELECT_RARE1 @40 // ~Ugh! I don't believe you properly appreciate the sacrifices I am making to help you.~
  SAY SELECT_RARE2 @41 // ~*Sigh* How may I assist you?~
  SAY REACT_TO_DIE_GENERAL @42 // ~Another incompetent peasant dead.~ 
  
// JOURNAL ///

ADD_JOURNAL TITLE (%Vienxay's Vengeance%) @10021 @10022 @10023 @10024 @10025 @10026  USING ~Vienxay/tra/english/Vien_bg1.tra~
ADD_JOURNAL TITLE (@152) @153 @154 @155 USING ~Vienxay/tra/english/Vien_bg1.tra~ // SoD Quest. 
////
 
APPEND ~pdialog.2da~ ~X3VIEN X3VienP X3VienJ X3vienD~
  UNLESS ~X3VIEN~

APPEND ~interdia.2da~ ~X3VIEN X3VienB~
  UNLESS ~X3VIEN~
  
///////////////////////////////////////////////
///////////////////////////////////////////////


      
ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
// Area scripts
  EXTEND_TOP ~_ar2300.bcs~ ~Vienxay/Script/ar2300.baf~
    EVALUATE_BUFFER
		      EXTEND_TOP ~_ar4801.bcs~ ~Vienxay/Script/ar4801.baf~
        EVALUATE_BUFFER
			EXTEND_TOP ~_ar1600.bcs~ ~Vienxay/Script/ar1600.baf~
        EVALUATE_BUFFER
			EXTEND_TOP ~_ar2100.bcs~ ~Vienxay/Script/ar2100.baf~
        EVALUATE_BUFFER	

END ELSE BEGIN 
  ACTION_IF FILE_EXISTS_IN_GAME ~AR7200.are~ THEN BEGIN
 //Area scripts
    EXTEND_TOP ~AR6800.bcs~ ~Vienxay/Script/ar2300.baf~
      EVALUATE_BUFFER
		      EXTEND_TOP ~AR3701.bcs~ ~Vienxay/Script/ar4801.baf~
        EVALUATE_BUFFER
			EXTEND_TOP ~AR8400.bcs~ ~Vienxay/Script/ar1600.baf~
        EVALUATE_BUFFER
			EXTEND_TOP ~AR8800.bcs~ ~Vienxay/Script/ar2100.baf~
        EVALUATE_BUFFER		  

  END ELSE BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~neera.dlg~ THEN BEGIN

  // Area scripts
      EXTEND_TOP ~ar2300.bcs~ ~Vienxay/Script/ar2300.baf~
        EVALUATE_BUFFER
		      EXTEND_TOP ~ar4801.bcs~ ~Vienxay/Script/ar4801.baf~
        EVALUATE_BUFFER
			EXTEND_TOP ~ar1600.bcs~ ~Vienxay/Script/ar1600.baf~
        EVALUATE_BUFFER
			EXTEND_TOP ~ar2100.bcs~ ~Vienxay/Script/ar2100.baf~
        EVALUATE_BUFFER

END        
END
END
      



///////////////////////////////////////////////
/* We do not have music right now. 
We are going to comment out all of this, but we will add music in the future, so I am going to set it up.
If we ever do, we can just remove this text and the two asterisks and slashes, and make sure the file names are set up right.
COPY_EXISTING ~songlist.2da~ ~override~
REPLACE_TEXTUALLY
~0 No_music ****~
~0 X31VieMU X31VieMU.MUS~

COPY ~Vienxay\X31VieMU.mus~ ~music/X31VieMU.mus~
*/
///////////////////////////////////////////////



// This code is also used by BG2 Fixpack - it would help you to run custom music for your NPC from "a clean slate", installing "blank" music you could run it with. Not everyone installs the Fixpack, though, so here we go:
// Feel free to skip this code if you're not using any music in your mod, only your NPC's voice.
/*COPY_EXISTING ~songlist.2da~ ~override~
SET_2DA_ENTRY 2 1 2 ~Blank~
SET_2DA_ENTRY 2 2 2 ~Blank.mus~*/

//COPY ~Vienxay/Audio~ ~override~ // This add's Vienxay's sole attack file (For the moment). 

/////////////////////////////////////////////////
/////////////////////////////////////////////////



    
COMPILE EVALUATE_BUFFER ~Vienxay/Script/X3VIEN.baf~
COMPILE EVALUATE_BUFFER ~Vienxay/Script/X3VAPR.baf~
COMPILE EVALUATE_BUFFER ~Vienxay/Script/X3VAPR2.baf~
COMPILE EVALUATE_BUFFER ~Vienxay/Script/X3VAPR3.baf~
COMPILE EVALUATE_BUFFER ~Vienxay/Script/X3VAPR4.baf~
COMPILE EVALUATE_BUFFER ~Vienxay/Script/X3VAPR5.baf~
COMPILE EVALUATE_BUFFER ~Vienxay/Script/X3VMAS.baf~
/////////////////////////////////////////////////
/////////////////////////////////////////////////
COMPILE EVALUATE_BUFFER ~Vienxay/dialogue/X3VIEN.d~
COMPILE EVALUATE_BUFFER ~Vienxay/dialogue/X3VIENj.d~
COMPILE EVALUATE_BUFFER ~Vienxay/dialogue/X3VIENb.d~
COMPILE EVALUATE_BUFFER ~Vienxay/dialogue/X3VIENp.d~
COMPILE EVALUATE_BUFFER ~Vienxay/dialogue/X3VAPR.d~
COMPILE EVALUATE_BUFFER ~Vienxay/dialogue/X3VAPR2.d~
COMPILE EVALUATE_BUFFER ~Vienxay/dialogue/X3VMAS.d~

ACTION_IF FILE_EXISTS_IN_GAME ~neera.dlg~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~Vienxay/dialogue/bgee.d~
END

/////////////////////////////////////////////////

/* Creatures */ 
COPY ~%MOD_FOLDER%/cre/X3VAPR2.cre~ ~override/X3VAPR2.cre~
SAY NAME1 @51
SAY NAME2 @51 
  WRITE_ASCII 0x248 ~X3VAPR2~ #8  //override script
  WRITE_ASCII 0x280 ~X3VAPR2~ #32  //DV
  WRITE_ASCII 0x2cc ~X3VAPR2~ #8   //pre-joining dialogue file
COPY ~%MOD_FOLDER%/cre/X3VAPR3.cre~ ~override/X3VAPR3.cre~
SAY NAME1 @51
SAY NAME2 @51 
  WRITE_ASCII 0x248 ~X3VAPR3~ #8  //override script
  WRITE_ASCII 0x280 ~X3VAPR3~ #32  //DV
COPY ~%MOD_FOLDER%/cre/X3VAPR4.cre~ ~override/X3VAPR4.cre~
SAY NAME1 @51
SAY NAME2 @51 
  WRITE_ASCII 0x248 ~X3VAPR4~ #8  //override script
  WRITE_ASCII 0x280 ~X3VAPR4~ #32  //DV
COPY ~%MOD_FOLDER%/cre/X3VAPR5.cre~ ~override/X3VAPR5.cre~
SAY NAME1 @51
SAY NAME2 @51 
  WRITE_ASCII 0x248 ~X3VAPR5~ #8  //override script
  WRITE_ASCII 0x280 ~X3VAPR5~ #32  //DV  
COPY ~%MOD_FOLDER%/cre/X3VAPR.cre~ ~override/X3VAPR.cre~
  SAY NAME1 @49 //~Lacius~
  SAY NAME2 @49 //~Lacius~
  WRITE_ASCII 0x248 ~X3VAPR~ #8  //override script
  WRITE_ASCII 0x280 ~X3VAPR~ #32  //DV
  WRITE_ASCII 0x2cc ~X3VAPR~ #8   //pre-joining dialogue file

COPY ~%MOD_FOLDER%/cre/X3VMAS.cre~ ~override/X3VMAS.cre~
  SAY NAME1 @50 //~Shadow Master Luna~
  SAY NAME2 @50 //~Shadow Master Luna~
  WRITE_ASCII 0x248 ~X3VMAS~ #8  //override script
  WRITE_ASCII 0x280 ~X3VMAS~ #32  //DV
  WRITE_ASCII 0x2cc ~X3VMAS~ #8   //pre-joining dialogue file
// Cross Mod, currently we have none, below is example template if Vienxay was a seperate mod and not with Vienxay.

ACTION_IF FILE_EXISTS_IN_GAME ~x3milyB.dlg~
THEN BEGIN
  PRINT @47 // Adding crossmod banter between Emily and Vienxay
COMPILE ~Vienxay/dialogue/CrossMod/Emily.d~
END 

ACTION_IF FILE_EXISTS_IN_GAME ~x3KaleB.dlg~
THEN BEGIN
  PRINT @48 // Adding crossmod banter between Vienxay and Kale
COMPILE ~Vienxay/dialogue/CrossMod/Kale.d~
END 

ACTION_IF FILE_EXISTS_IN_GAME ~x3RecB.dlg~
THEN BEGIN
  PRINT @52 // Adding crossmod banter between Vienxay and Recorder
COMPILE ~Vienxay/dialogue/CrossMod/Recorder.d~
END 

ACTION_IF FILE_EXISTS_IN_GAME ~x3RecB.dlg~
THEN BEGIN
  PRINT @53 // Adding crossmod banter between Vienxay and Helga
COMPILE ~Vienxay/dialogue/CrossMod/Helga.d~
END 

ACTION_IF FILE_EXISTS_IN_GAME ~BC0Drake.dlg~
THEN BEGIN
  PRINT @57 // Adding crossmod banter between Vienxay and Drake
COMPILE ~Vienxay/dialogue/CrossMod/Drake.d~
END 

//TO MAKE IT DISPLAY RIGHT IN WEIDU

LOAD_TRA ~Vienxay/translations/English/Vienxay.tra~
